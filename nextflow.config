
params {
    // pipeline input args
    prefix              = null
    species_opts        = '--allow-extra-chr --chr-set 29'
    steps               = 1000
    individuals         = [20, 50, 100]

    // module params
    pgdspider_spid      = "$baseDir/assets/PED_GENEPOP.spid"
    save_plink_subset   = false
    save_genepop        = false

    publish_dir_mode    = 'link'
    outdir              = './results'
    tracedir            = "${params.outdir}/pipeline_info"

    // Max resource options
    // Defaults only, expecting to be overwritten
    max_memory          = '128.GB'
    max_cpus            = 32
    max_time            = '240.h'
}

process {
    cpus   = { check_max( 1    * task.attempt, 'cpus'   ) }
    memory = { check_max( 6.GB * task.attempt, 'memory' ) }
    time   = { check_max( 4.h  * task.attempt, 'time'   ) }

    errorStrategy = { task.exitStatus in [104, 134, 137, 139, 140, 143] ? 'retry' : 'finish' }
    maxRetries    = 2
    maxErrors     = '-1'

    publishDir = [
        path: { "${params.outdir}/${task.process.tokenize(':')[-1].tokenize('_')[0].toLowerCase()}" },
        mode: params.publish_dir_mode,
        saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
    ]

    withLabel:process_single {
        cpus   = { check_max( 1                  , 'cpus'    ) }
        memory = { check_max( 4.GB * task.attempt, 'memory'  ) }
        time   = { check_max( 12.h * task.attempt, 'time'    ) }
    }

    withLabel:process_low {
        cpus   = { check_max( 2     * task.attempt, 'cpus'    ) }
        memory = { check_max( 4.GB  * task.attempt, 'memory'  ) }
        time   = { check_max( 4.h   * task.attempt, 'time'    ) }
    }

    withLabel:process_high_memory {
        memory = { check_max( 16.GB + 8.GB * task.attempt, 'memory' ) }
    }

    withLabel:error_retry {
        errorStrategy = 'retry'
        maxRetries    = 5
    }

    withName: PLINK_SUBSET {
        publishDir = [
            path: { "${params.outdir}/plink_subset/${meta.individuals}_individuals/${meta.id}" },
            mode: params.publish_dir_mode,
            pattern: "*.{map,ped}",
            enabled: params.save_plink_subset
        ]
    }

    withName: PED2GENEPOP {
        publishDir = [
            path: { "${params.outdir}/ped2genepop/${meta.individuals}_individuals/${meta.id}" },
            mode: params.publish_dir_mode,
            pattern: "*.{txt}",
            enabled: params.save_genepop
        ]
    }

    withName: LDNE {
        publishDir = [
            path: { "${params.outdir}/NeEstimator2.X/${meta.individuals}_individuals/${meta.id}" },
            mode: params.publish_dir_mode,
            pattern: "*.{txt}"
        ]
    }

    withName: SUMMARIZE {
        publishDir = [
            path: { "${params.outdir}/summarize" },
            mode: params.publish_dir_mode,
            pattern: "*.{csv}"
        ]
    }
}


profiles {
    conda {
        params.enable_conda     = true
        docker.enabled          = false
        singularity.enabled     = false
    }
    docker {
        docker.enabled          = true
        docker.userEmulation    = true
        singularity.enabled     = false
    }
    singularity {
        singularity.enabled     = true
        singularity.autoMounts  = true
        docker.enabled          = false
    }
    test {
        params.steps            = 5
        params.prefix           = 'testdata/murciano_test'

        // limiting resources
        params.max_cpus         = 2
        params.max_memory       = 6.GB
        params.max_time         = '6.h'
    }
    local {
        process.executor        = 'local'
    }
    slurm {
        process.executor        = 'slurm'
    }
}

manifest {
    name            = 'cnr-ibba/nf-neestimator'
    author          = 'Paolo Cozzi <bunop@libero.it>'
    homePage        = 'https://github.com/cnr-ibba/nf-neestimator'
    description     = 'A nextflow pipeline for NeEstimator2.X calculations with bootstrapping'
    mainScript      = 'main.nf'
    nextflowVersion = '!>=22.10.1'
    version = '0.2.0'
}

// Export these variables to prevent local Python/R libraries from conflicting with those in the container
env {
    PYTHONNOUSERSITE = 1
    R_PROFILE_USER   = "/.Rprofile"
    R_ENVIRON_USER   = "/.Renviron"
}

// Capture exit codes from upstream processes when piping
process.shell = ['/bin/bash', '-euo', 'pipefail']

def trace_timestamp = new java.util.Date().format( 'yyyy-MM-dd_HH-mm-ss')
timeline {
    enabled = true
    file    = "${params.tracedir}/execution_timeline_${trace_timestamp}.html"
}
report {
    enabled = true
    file    = "${params.tracedir}/execution_report_${trace_timestamp}.html"
}
trace {
    enabled = true
    file    = "${params.tracedir}/execution_trace_${trace_timestamp}.txt"
}
dag {
    enabled = true
    file    = "${params.tracedir}/pipeline_dag_${trace_timestamp}.svg"
}

// Function to ensure that resource requirements don't go beyond
// a maximum limit
def check_max(obj, type) {
    if (type == 'memory') {
        try {
            if (obj.compareTo(params.max_memory as nextflow.util.MemoryUnit) == 1)
                return params.max_memory as nextflow.util.MemoryUnit
            else
                return obj
        } catch (all) {
            println "   ### ERROR ###   Max memory '${params.max_memory}' is not valid! Using default value: $obj"
            return obj
        }
    } else if (type == 'time') {
        try {
            if (obj.compareTo(params.max_time as nextflow.util.Duration) == 1)
                return params.max_time as nextflow.util.Duration
            else
                return obj
        } catch (all) {
            println "   ### ERROR ###   Max time '${params.max_time}' is not valid! Using default value: $obj"
            return obj
        }
    } else if (type == 'cpus') {
        try {
            return Math.min( obj, params.max_cpus as int )
        } catch (all) {
            println "   ### ERROR ###   Max cpus '${params.max_cpus}' is not valid! Using default value: $obj"
            return obj
        }
    }
}
